set encoding=utf-8
set nocompatible
set termguicolors

if $VIRTUAL_ENV != ''
  let g:python3_host_prog = $VIRTUAL_ENV . '/bin/python3'
  let g:ycm_python_interpreter_path = $VIRTUAL_ENV . '/bin/python3'
endif


let mapleader = ","

" Switch syntax highlighting on, when the terminal has colors
syntax on
filetype plugin indent on

set cursorline
set nu
set ruler
set softtabstop=2
set shiftwidth=2
set linebreak
set expandtab
set foldmethod=indent
" foldlevel初始值，高于此值的fold会默认关闭
set foldlevelstart=4
" 搜索逐字符高亮
set hlsearch
set incsearch
set completeopt=menu,menuone,preview,noselect,noinsert

" 配置window style {{{
set fillchars+=vert:\ 
highlight VertSplit ctermfg=LightGreen
highlight StatusLine ctermfg=Green
highlight StatusLineNC ctermfg=LightGreen
" }}}

" Color theme {{{
set background=dark
" }}}

" 配置tag {{{
set tags=./.tags;
" }}}


" 配置gutentags {{{
" gutentags 搜索工程目录的标志，碰到这些文件/目录名就停止向上一级目录递归
let g:gutentags_project_root = ['.root', '.svn', '.git', '.hg', '.project']
" 所生成的数据文件的名称
let g:gutentags_ctags_tagfile = '.tags'
" 配置 ctags 的参数
let g:gutentags_ctags_extra_args = ['--fields=+niazS', '--extra=+q']
let g:gutentags_ctags_extra_args += ['--c++-kinds=+px']
let g:gutentags_ctags_extra_args += ['--c-kinds=+px']
let g:gutentags_ctags_exclude = ['node_modules']

set statusline+=%{gutentags#statusline()}
" }}}


" 配置NERDTree {{{
let NERDTreeShowHidden=1
noremap <C-n> :NERDTreeToggle<CR>
noremap <leader>e :NERDTreeFind<CR>
" }}}


" 配置ctrlp {{{
let g:ctrlp_map = '<c-p>'
let g:ctrlp_custom_ignore = '\v[\/](node_modules|(\.(swp|ico|git|svn)))$'
let g:ctrlp_show_hidden = 1
" }}}


" 配置ale {{{
nnoremap <leader>] :ALEGoToDefinition<CR>
nnoremap <leader>^ :ALEFindReferences<CR>
" ycm semantic completion does not quite work, so i'm gonna: use ale to do lint and
" semantic completion, use ycm to do tag based completion
let g:ale_completion_enabled = 1
let g:ale_set_highlights = 0 " Disable highligting
" let g:ale_set_signs = 0
let g:ale_linters = {
      \ 'javascript': ['eslint', 'tsserver'],
      \ 'cpp': ['clangd'],
      \ }
let g:ale_cpp_clangd_executable = '/usr/local/opt/llvm/bin/clangd'
" }}}


" 配置SirVer/ultisnips, keep ycm ultisnips from fighting {{{
" make YCM compatible with UltiSnips (using supertab)
let g:ycm_key_list_select_completion = ['<C-n>', '<Down>']
let g:ycm_key_list_previous_completion = ['<C-p>', '<Up>']
let g:SuperTabDefaultCompletionType = '<C-n>'

" better key bindings for UltiSnipsExpandTrigger
let g:UltiSnipsExpandTrigger = "<tab>"
let g:UltiSnipsJumpForwardTrigger = "<tab>"
let g:UltiSnipsJumpBackwardTrigger = "<s-tab>"
" }}}


" Config indent-guides {{{
let g:indent_guides_start_level = 1
let g:indent_guides_guide_size = 1
highlight IndentGuidesOdd  ctermbg=DarkGray
highlight IndentGuidesEven ctermbg=LightGray
"}}}

" 配置 vim-fugitive {{{
nnoremap <leader>ge :Gedit<CR>
nnoremap <leader>gb :Gblame<CR>
nnoremap <leader>gbr :Gbrowse<CR>
vnoremap <leader>gbr :Gbrowse<CR>
nnoremap <leader>gs :Gstatus<CR>
nnoremap <leader>ga :Git add -p<CR>
nnoremap <leader>gc :Gcommit -v<CR>
nnoremap <leader>gcm :Gcommit --amend<CR>
nnoremap <leader>gp :Gpush<CR>
nnoremap <leader>gvd :Gvdiff<CR>
nnoremap <leader>gd2 :Gvdiff @<CR>
nnoremap <leader>gdt :Git difftool --cached<CR>
nnoremap <leader>gwr :Gwrite<CR>
nnoremap <leader>grd :Gread<CR>
nnoremap <leader>gll :Git ll<CR>
nnoremap <leader>glg :Git lg<CR>
nnoremap <leader>grm :Gremove<CR>
nnoremap <leader>gdel :Gdelete<CR>
nnoremap <leader>gmv :Gmove % 
nnoremap <leader>grst :Git reset %<CR>
nnoremap <leader>grmt :r ! git remote get-url origin<CR>
nnoremap <leader>gph :Gpush
nnoremap <leader>gfh :Gfetch<CR>
" }}}

" key bindings {{{
nnoremap <leader>msg :messages<CR>
nnoremap <leader>ev :vsplit $MYVIMRC<CR>
nnoremap <leader>sv :source $MYVIMRC<CR>

nnoremap <leader>zz :normal! ZZ<CR>
nnoremap <leader>cz :normal! <C-z><CR>
nnoremap <leader>qa :qa<CR>
nnoremap <leader>qq :q!<CR>
nnoremap <leader>ww :w<CR>
nnoremap <leader>wa :wa<CR>
nnoremap <leader>wq :wq<CR>
nnoremap <leader>vs :vsp ~/
nnoremap <leader>ed :edit 
nnoremap <leader>11 :!
nnoremap <leader>1r :!r 
nnoremap <leader>st :Start! 
nnoremap <leader>dg :diffget<CR>

" 编辑快捷键
" 快速保存
inoremap <Esc><Esc> <Esc>:w<CR>
" 关闭高亮
nnoremap <leader>nh :noh<CR>
" 向前/后跳转至{/}符号
map [[ ?{<CR>w99[{
map ][ /}<CR>b99]}
map ]] j0[[%/{<CR>
map [] k$][%?}<CR>

nnoremap <leader>p :set paste<CR>
nnoremap <leader>np :set nopaste<CR>

command! -nargs=1 CMDGoNextFold call GoNextFold(<args>)
command! -nargs=1 CMDGoPrevFold call GoPrevFold(<args>)
nnoremap <leader>zj :call :<C-U>CMDGoNextFold(v:count1)<CR>
nnoremap <leader>zk :call :<C-U>CMDGoPrevFold(v:count1)<CR>

xnoremap @ :<C-u>call ExecuteMacroOverVisualRange()<CR>

function! ExecuteMacroOverVisualRange()
  echo "@".getcmdline()
  execute ":'<,'>normal @".nr2char(getchar())
endfunction

function! GoNextFold(cnt)
  if a:cnt ==# 0
    return
  endif
  let l:initial_line = line('.')
  let l:lnum = foldclosedend(l:initial_line) + 1
  if l:lnum ==# 0
    let l:lnum = l:initial_line
  endif 
  let l:end_line = line('$')
  while foldclosed(l:lnum) ==# -1 && l:lnum <=# l:end_line
    let l:lnum += 1
  endwhile
  if l:lnum ># l:end_line
    return
  endif
  execute 'normal! ' . l:lnum . 'G'
  call GoNextFold(a:cnt - 1)
endfunction

function! GoPrevFold(cnt)
  if a:cnt ==# 0
    return
  endif
  let l:initial_line = line('.')
  let l:lnum = foldclosed(l:initial_line) - 1
  if l:lnum ==# -2
    let l:lnum = l:initial_line
  endif 
  let l:end_line = 1
  while foldclosed(l:lnum) ==# -1 && l:lnum >=# l:end_line
    let l:lnum -= 1
  endwhile
  if l:lnum <# l:end_line
    return
  endif
  execute 'normal! ' . l:lnum . 'G'
  call GoPrevFold(a:cnt - 1)
endfunction

noremap <C-h> <C-w>h
noremap <C-j> <C-w>j
noremap <C-k> <C-w>k
noremap <C-l> <C-w>l

map <Up> gk
map <Down> gj
vnoremap // y/<C-R>"<CR>
" }}}


" 自定义命令 {{{
command! -nargs=+ -complete=file GR call CGrepSourceCode(<f-args>)
command! -nargs=+ -complete=file GR2 call GrepSourceCodeRaw(<f-args>)
command! -complete=file GR3 call VGrepSourceCode()
" 根据正则表达式匹配，其他的是匹配原始字符
command! -nargs=+ -complete=file GRE call GrepSourceCode(<f-args>)

nnoremap <leader>cgr :GR2 <cword> .<CR>
vnoremap <leader>vgr :<C-u>GR3<CR>

function! CGrepSourceCode(...)
  let l:word = expand('<cword>')
  let l:args = a:000[0:]
  let l:prepended = reverse(add(reverse(l:args), l:word))
  call call(function('GrepSourceCodeRaw'), l:prepended)
endfunction

function! VGrepSourceCode()
  let l:word = s:get_visual_selection()
  call GrepSourceCodeRaw(shellescape(l:word), '.')
endfunction

function! GetGrepSourceCode(opts)
  return 'grep -R '
        \ . '--exclude-dir .git '
        \ . '--exclude-dir node_modules '
        \ . '--exclude-dir tmp '
        \ . '--exclude-dir TMP '
        \ . '--exclude-dir build '
        \ . '--exclude-dir built '
        \ . '--exclude .tags '
        \ . '--exclude *.pyc '
        \ . '--exclude *.swp '
        \ . a:opts
        \ . ' '
endfunction

function! GrepSourceCodeRaw(...)
  execute GetGrepSourceCode('')
        \ . join(a:000)
endfunction

function! GrepSourceCode(...)
  execute GetGrepSourceCode('-E')
        \ . join(a:000)
endfunction
" }}}


" 封装的通用函数 {{{
" 代码搬运自https://stackoverflow.com/questions/1533565/how-to-get-visually-selected-text-in-vimscript/6271254#6271254
function! s:get_visual_selection()
  " Why is this not a built-in Vim script function?!
  let [line_start, column_start] = getpos("'<")[1:2]
  let [line_end, column_end] = getpos("'>")[1:2]
  let lines = getline(line_start, line_end)
  if len(lines) == 0
    return ''
  endif
  let lines[-1] = lines[-1][: column_end - (&selection == 'inclusive' ? 1 : 2)]
  let lines[0] = lines[0][column_start - 1:]
  return join(lines, "\n")
endfunction
" }}}


" 自动命令 ---------------------- {{{
augroup mygroup
  autocmd!
  autocmd FileType vim setlocal foldmethod=marker
  " 打开文件时光标自动定位到上次退出时的位置
  autocmd BufReadPost *
        \ if line("'\"") > 1 && line("'\"") <= line("$") && &ft !~# 'commit'
        \ |   exe "normal! g`\""
        \ | endif
augroup END
" }}}


" emmet-vim {{{
let g:user_emmet_install_global = 0
let g:user_emmet_settings = {
      \  'javascript.jsx' : {
      \      'extends' : 'jsx',
      \  },
      \}
autocmd FileType html,css,javascript,typescript,jsx EmmetInstall
" }}}


" 配置fugitive-gitlab {{{
let g:fugitive_gitlab_domains = ['https://code.smartstudy.com/']
" }}}


" 配置tagbar {{{
let g:tagbar_autoclose = 1
noremap <silent> <F8> :TagbarToggle<CR>
" }}}


" vim-plug插件 {{{
" plug begin with given dir; add common plugins
function! StartPlug(plugInDir)
  call plug#begin(a:plugInDir)
  Plug 'scrooloose/nerdtree', { 'on':  'NERDTreeToggle' }
  Plug 'ludovicchabant/vim-gutentags'
  Plug 'w0rp/ale'
  Plug 'bestofsong/vimconfig'
  Plug 'leafgarland/typescript-vim'
  Plug 'kien/ctrlp.vim'
  Plug 'godlygeek/tabular'
  Plug 'wellle/targets.vim'
  Plug 'easymotion/vim-easymotion'
  Plug 'tpope/vim-fugitive'
  Plug 'SirVer/ultisnips'
  Plug 'honza/vim-snippets'
  Plug 'ervandew/supertab'
  Plug 'tpope/vim-commentary'
  Plug 'tpope/vim-surround'
  Plug 'nathanaelkane/vim-indent-guides'
  Plug 'tpope/vim-dispatch'
  Plug 'morhetz/gruvbox'
  Plug 'pangloss/vim-javascript'
  Plug 'mxw/vim-jsx'
  Plug 'mattn/emmet-vim', { 'commit': 'd698f1658770ca5fa58c87e80421c8d65bbe9065' }
  Plug 'ap/vim-css-color'
  Plug 'ekalinin/Dockerfile.vim'
  Plug 'shumphrey/fugitive-gitlab.vim'
  Plug 'tpope/vim-rhubarb'
  Plug 'majutsushi/tagbar'
  Plug 'jiangmiao/auto-pairs'
endfunction

function! EndPlug()
  call plug#end()
endfunction
" }}}
