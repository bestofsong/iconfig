" setup s:IDE_CONF map used to change behavior according to WS_IDE value {{{
let s:IDE_CONF_MAP = {
      \ 'xvim': { 'color_scheme': 0 }
      \ }

if exists('$WS_IDE')
let s:IDE_CONF = s:IDE_CONF_MAP[$WS_IDE]
endif

" 检查是否为该ide设置了conf_name配置
function! s:ide_specified(conf_name)
  if !exists('s:IDE_CONF')
    return 0
  endif
  return has_key(s:IDE_CONF, a:conf_name)
endfunction
" s:ide_specified为1的情况下返回该配置的值
function! s:ide_use(conf_name)
  return s:IDE_CONF[a:conf_name]
endfunction
" }}}


" load my_func.vim
let s:func_file = expand('<sfile>:p:h') . '/my_func.vim'
:execute 'source ' . s:func_file


" 脚本变量 {{{
let g:WS_RIPGREP_GLOB = '--glob !.git/ '
      \ . '--glob !node_modules/ '
      \ . '--glob !tmp/ '
      \ . '--glob !TMP/ '
      \ . '--glob !build/ '
      \ . '--glob !built/ '
      \ . '--glob !.tags '
      \ . '--glob !.tags.temp '
      \ . '--glob !*.pyc '
      \ . '--glob !*.swp '
      \ . '--glob !compile_commands.json '
      \ . '--glob !*.temp '
" }}}


" vim 内置选项等 {{{
set encoding=utf-8
set nocompatible
set termguicolors
set history=10000
set exrc
set nowrapscan
" Switch syntax highlighting on, when the terminal has colors
syntax on
filetype plugin indent on

" 否则报错
if !has('nvim')
  packadd! matchit
endif

if match(&matchpairs, '<:>') == -1
  set matchpairs+=<:>
endif

let mapleader = ","

set cursorline
set nu
set ruler
set softtabstop=2
set shiftwidth=2
set linebreak
set expandtab
set foldmethod=indent
" foldlevel初始值，高于此值的fold会默认关闭
set foldlevelstart=4
" 搜索逐字符高亮
set hlsearch
set incsearch
set completeopt=menu,menuone,preview,noselect,noinsert
let &grepprg='rg --vimgrep --no-heading --no-ignore --hidden ' . g:WS_RIPGREP_GLOB

function! WS_reload_grepprg()
  let &grepprg='rg --vimgrep --no-heading --no-ignore --hidden ' . g:WS_RIPGREP_GLOB
  let $FZF_DEFAULT_COMMAND='rg --files --no-ignore --hidden --follow ' . g:WS_RIPGREP_GLOB
endfunction

" 配置window style
set fillchars+=vert:\ 
" set statusline=%<%f%Y%=\ [%n%1*%M%R%*]\ %-19(%3l,%02c%03V%)'%02b'
" }}}


" 配置tag {{{
set tags=./.tags;
" }}}


" 配置gutentags {{{
" gutentags 搜索工程目录的标志，碰到这些文件/目录名就停止向上一级目录递归
let g:gutentags_project_root = ['.root', '.svn', '.git', '.hg', '.project']
" 所生成的数据文件的名称
let g:gutentags_ctags_tagfile = '.tags'
" 配置 ctags 的参数
let g:gutentags_ctags_extra_args = ['--fields=+niazS', '--extra=+q']
let g:gutentags_ctags_extra_args += ['--c++-kinds=+px']
let g:gutentags_ctags_extra_args += ['--c-kinds=+px']
let g:gutentags_ctags_exclude = ['node_modules']
" }}}


" 配置NERDTree {{{
let NERDTreeShowHidden=1
noremap <C-n> :NERDTreeToggle<CR>
noremap <leader>ee :NERDTree <bar> NERDTreeFind <C-R>%<CR>
" }}}


" 配置ycm {{{
if $VIRTUAL_ENV != ''
  let g:python3_host_prog = $VIRTUAL_ENV . '/bin/python3'
  let g:ycm_python_interpreter_path = $VIRTUAL_ENV . '/bin/python3'
endif

function! s:MyYCMTheme()
  highlight YcmErrorSection ctermbg=DarkRed
  highlight YcmErrorSign ctermbg=DarkRed
endfunction

noremap <leader>ja :YcmCompleter GoTo<CR>
noremap <leader>jr :YcmCompleter GoToReferences<CR>
noremap <leader>ef :YcmShowDetailedDiagnostic<CR>
noremap <leader>ycmdbg :YcmDebugInfo<CR>
let s:ncpucores = str2nr(system('getconf _NPROCESSORS_ONLN 2>/dev/null || sysctl -n hw.ncpu | sed "s/[^0-9]//g"'))
let g:ycm_clangd_args = ['-completion-style=detailed', '-log=error', '-pretty', '-limit-results=100', '-j=' . s:ncpucores, '-pch-storage=disk']
" Let clangd fully control code completion
let g:ycm_clangd_uses_ycmd_caching = 0
" Use installed clangd, not YCM-bundled clangd which doesn't get updates.
let g:ycm_clangd_binary_path = '/usr/local/opt/llvm/bin/clangd'
let g:ycm_autoclose_preview_window_after_insertion = 1
" }}}


" 配置ale {{{
nnoremap <leader>] :ALEGoToDefinition<CR>
nnoremap <leader>\ :ALEGoToDefinitionInVSplit<CR>
nnoremap <leader>^ :ALEFindReferences<CR>
" ycm semantic completion does not quite work, so i'm gonna: use ale to do lint and
" semantic completion, use ycm to do tag based completion
let g:ale_completion_enabled = 1
let g:ale_set_highlights = 0 " Disable highligting
" let g:ale_set_signs = 0
let g:ale_linters = {
      \ 'javascript': ['eslint', 'tsserver'],
      \ 'cpp': ['clangd'],
      \ 'objc': ['clangd'],
      \ 'objcpp': ['clangd'],
      \ }
let g:ale_cpp_clangd_executable = '/usr/local/opt/llvm/bin/clangd'
let g:ale_objc_clangd_executable = "'/usr/local/opt/llvm/bin/clangd', '-completion-style=detailed', '-log=error', '-pretty', '-limit-results=100', '-j=12', '-pch-storage=disk', '-header-insertion-decorators=0'"
let g:ale_objcpp_clangd_executable = "'/usr/local/opt/llvm/bin/clangd',  '-completion-style=detailed', '-log=error', '-pretty', '-limit-results=100', '-j=12', '-pch-storage=disk', '-header-insertion-decorators=0'"
" }}}


" 配置SirVer/ultisnips {{{
" better key bindings for UltiSnipsExpandTrigger
let g:UltiSnipsExpandTrigger = "<C-l>"
let g:UltiSnipsJumpForwardTrigger = "<C-j>"
let g:UltiSnipsJumpBackwardTrigger = "<C-k>"
" }}}


" Config indentLine {{{
let g:indentLine_char_list = ['|', '¦', '┆',  '↓', '↑', '†', '·', '‖', 'ˇ', '┊']
nnoremap <leader>ig :IndentLinesToggle<CR>
let g:indentLine_concealcursor = 'c'
" }}}


" 自动命令 ---------------------- {{{
" 该函数执行的时候vimrc可能还没有执行过，此时延时递归，否则立即执行
function! WSWrapYcmRestartServer(_)
  if v:vim_did_enter
    :silent YcmRestartServer
  else
    call timer_start(1000, 'WSWrapYcmRestartServer')
  endif
endfunction

augroup mygroup
  autocmd!
  autocmd BufNewFile,BufRead *.ejs set filetype=html
  autocmd FileType vim setlocal foldmethod=marker
  " 打开文件时光标自动定位到上次退出时的位置
  autocmd BufReadPost *
        \ if line("'\"") > 1 && line("'\"") <= line("$") && &ft !~# 'commit'
        \ |   exe "normal! g`\""
        \ | endif
  autocmd BufEnter COMMIT_EDITMSG let g:ycm_collect_identifiers_from_comments_and_strings = 1 | :silent call WSWrapYcmRestartServer(0)
  autocmd BufLeave COMMIT_EDITMSG let g:ycm_collect_identifiers_from_comments_and_strings = 0 | :silent call WSWrapYcmRestartServer(0)
augroup END
" }}}


" emmet-vim {{{
let g:user_emmet_install_global = 0
let g:user_emmet_settings = {
      \  'javascript.jsx' : {
      \      'extends' : 'jsx',
      \  },
      \}
autocmd mygroup FileType html,css,javascript,typescript,jsx,xml EmmetInstall
" }}}


" 配置fugitive-gitlab {{{
let g:fugitive_gitlab_domains = ['https://code.smartstudy.com/']
" }}}


" 配置tagbar {{{
let g:tagbar_autoclose = 1
noremap <silent> <F8> :TagbarToggle<CR>
" }}}


" 配置fzf {{{
" 环境变量
let $FZF_DEFAULT_COMMAND='rg --files --no-ignore --hidden --follow ' . g:WS_RIPGREP_GLOB
let g:fzf_buffers_jump = 1
nnoremap <silent> <C-p>p :Files<CR>
nnoremap <silent> <C-p>b :Buffers<CR>
nnoremap <silent> <C-p>l :Lines<CR>
" }}}


" {{{ colorscheme
function! s:load_custom_color_scheme()
  if s:ide_specified('color_scheme') && !s:ide_use('color_scheme')
    return
  endif
  " 依赖plugin的初始化
  set background=dark
  colorscheme gruvbox
  call s:MyYCMTheme()
endfunction
" }}}


" {{{
" let g:LanguageClient_serverCommands = {
"       \ 'rust': ['rustup', 'run', 'stable', 'rls'],
"       \ 'objc': ['/Users/ws2356/.vim/plugged/YouCompleteMe/third_party/ycmd/third_party/clangd/output/bin/clangd', '-log=error', '-pretty', '-limit-results=100', '-j=12', '-pch-storage=disk', '-header-insertion-decorators=0', '-resource-dir=/Users/ws2356/.vim/plugged/YouCompleteMe/third_party/ycmd/third_party/clang/lib/clang/8.0.0'],
"       \ }
" nnoremap <silent> <leader>K :call LanguageClient#textDocument_hover()<CR>
" nnoremap <silent> <leader>gd :call LanguageClient#textDocument_definition()<CR>
" nnoremap <silent> <F2> :call LanguageClient#textDocument_rename()<CR>
" set completefunc=LanguageClient#complete
" }}}


" vim-plug插件 {{{
" plug begin with given dir; add common plugins
function! StartPlug(plugInDir)
  call plug#begin(a:plugInDir)
  Plug 'scrooloose/nerdtree', { 'commit': '28eb47e2678cf629d92b4f1f00dd56cba22fc4ae' }
  Plug 'ludovicchabant/vim-gutentags', { 'commit': 'eecb136fae97e30d5f01e71f0d3b775c8b017385' }
  Plug 'Valloric/YouCompleteMe', { 'do': './install.py --clangd-completer --java-completer', 'commit': '04c3505129cd80b92f1b6177dca8aecc55cb0760' }
  Plug 'w0rp/ale', { 'commit': 'a5240009ba5ff22daad95c306f7dec372d46bda0' }
  Plug 'bestofsong/vimconfig', { 'commit': '5ba87562ecd3bb02e460fc350808fc19449621ec' }
  Plug 'leafgarland/typescript-vim', { 'commit': '7704fac2c765aaf975ad4034933bf63113dd4a64' }
  Plug 'godlygeek/tabular', { 'commit': '339091ac4dd1f17e225fe7d57b48aff55f99b23a' }
  Plug 'wellle/targets.vim', { 'commit': 'a79447f261e4b8b4327557aa03726f3849334b84' }
  Plug 'easymotion/vim-easymotion', { 'commit': '85e90c9759e14633d878ed534ef313876ab96555' }
  Plug 'tpope/vim-fugitive', { 'commit': '6d42c7df44aa20252e5dac747c3ac9fa7450b21b' }
  Plug 'SirVer/ultisnips', { 'commit': '1a99766b73783bafb08cfa07d7e29e5387c55189' }
  Plug 'honza/vim-snippets', { 'commit': 'a11cf5b47fcb9de72b5c8694a4e2fe2dca8c0ae7' }
  Plug 'tpope/vim-commentary', { 'commit': '141d9d32a9fb58fe474fcc89cd7221eb2dd57b3a' }
  Plug 'tpope/vim-surround', { 'commit': 'ca58a2d886cc18734c90c9665da4775d444b0c04' }
  Plug 'Yggdroot/indentLine', { 'commit': '47648734706fb2cd0e4d4350f12157d1e5f4c465' }
  Plug 'tpope/vim-dispatch', { 'commit': '488940870ab478cc443b06d5a62fea7ab999eabf' }
  Plug 'morhetz/gruvbox', { 'commit': 'cb4e7a5643f7d2dd40e694bcbd28c4b89b185e86' }
  Plug 'pangloss/vim-javascript', { 'commit': 'ee445807a71ee6933cd6cbcd74940bc288815793' }
  Plug 'mxw/vim-jsx', { 'commit': 'ffc0bfd9da15d0fce02d117b843f718160f7ad27' }
  Plug 'mattn/emmet-vim', { 'commit': 'd698f1658770ca5fa58c87e80421c8d65bbe9065' }
  Plug 'ap/vim-css-color', { 'commit': '8a84356d5319cad3da2835bd5fbc6318500f31ce' }
  Plug 'ekalinin/Dockerfile.vim', { 'commit': 'f647f90dcecb8738cd53ac8dd293d93570039e00' }
  Plug 'shumphrey/fugitive-gitlab.vim', { 'commit': '43a13dbbc9aae85338877329ed28c9e4d8488db1' }
  Plug 'jiangmiao/auto-pairs', { 'commit': '39f06b873a8449af8ff6a3eee716d3da14d63a76' }
  Plug 'vim-ruby/vim-ruby', { 'commit': '1aa8f0cd0411c093d81f4139d151f93808e53966' }
  Plug '/usr/local/opt/fzf'
  Plug 'junegunn/fzf.vim', { 'commit': '359a80e3a34aacbd5257713b6a88aa085337166f' }
  Plug 'vim-airline/vim-airline', { 'commit': 'c213f2ac44292a6c5548872e63acb0648cc07a9a' }
  Plug 'tpope/vim-rhubarb', { 'commit': 'c509c7eedeea641f5b0bdae708581ff610fbff5b' }
  Plug 'vim-scripts/MPage', { 'commit': 'c7915d434d66d51de6f7bb805f353946fd08a5de' }
  " 不好配置（不支持clangd compile db）
  " Plug 'jeaye/color_coded', { 'commit': '16e71d6f5850849c6ffc9a06a7c952e27d351866' }
  " 不好配置（不支持clangd compile db）
  " Plug 'rdnetto/YCM-Generator', { 'commit': '461e3042a8ecfafe53a61d8b81e67e6f7558c9ac' }
  " 没有效果
  " Plug 'msanders/cocoa.vim', { 'commit': '33faf435cf251ae23e0ee3500a0ce0eee6a1a94b' }
  " 没有效果
  " Plug 'b4winckler/vim-objc'
endfunction

function! EndPlug()
  call plug#end()
  call s:load_custom_color_scheme()
endfunction
" }}}


" load my_key_binding.vim
let s:my_key_binding_file = expand('<sfile>:p:h') . '/my_key_binding.vim'
:execute 'source ' . s:my_key_binding_file
let s:my_key_binding_shell_file = expand('<sfile>:p:h') . '/my_key_binding_shell.vim'
:execute 'source ' . s:my_key_binding_shell_file
